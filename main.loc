module root-r (*)

import root

type R => Filename = "character"
type R => Unit = "NULL"
type R => Int = "integer"
type R => Real = "numeric"
type R => Str = "character"
type R => Bool = "logical"
type R => (Map Str b) = "list" Str b
type R => (List a) = "list" a
type R => (Tuple2 a b) = "list" a b
type R => (Tuple3 a b c) = "list" a b c
type R => (Tuple4 a b c d) = "list" a b c d
type R => (Tuple5 a b c d e) = "list" a b c d e
type R => (Tuple6 a b c d e f) = "list" a b c d e f
type R => (Tuple7 a b c d e f g) = "list" a b c d e f g
type R => (Tuple8 a b c d e f g h) = "list" a b c d e f g h

--' An identity function used to force R usage
idr a :: a -> a

source R from "R/core.R"
    ( "morloc_idr" as idr
    , "morloc_zipWith" as zipWith
    , "morloc_fst" as fst
    , "morloc_snd" as snd
    , "morloc_toFst" as toFst
    , "morloc_toSnd" as toSnd
    , "morloc_fst" as fst3
    , "morloc_snd" as snd3
    , "morloc_thr" as thr3
    , "morloc_ifelse" as ifelse
    , "morloc_not" as not
    , "morloc_and" as and
    , "morloc_or" as or
    )

instance Eq a where
  -- this will work for any type that defines a '==' operator
  source R from "R/core.R" ("morloc_eq" as eq)

instance Ord a where
  -- this will work for any type that defines a '<=' operator
  source R from "R/core.R" ("morloc_le" as le)

instance Integral Int where
  source R from "R/core.R"
    ( "morloc_neg" as neg
    , "morloc_add" as add
    , "morloc_sub" as sub
    , "morloc_mul" as mul
    )
  zero = 0

instance Integral Real where
  source R from "R/core.R"
    ( "morloc_neg" as neg
    , "morloc_add" as add
    , "morloc_sub" as sub
    , "morloc_mul" as mul
    )
  zero = 0.0

instance Numeric Real where
  source R from "R/core.R"
    ( "morloc_inv" as inv
    , "morloc_div" as div
    , "morloc_exp" as exp
    , "morloc_log" as log
    )
  one = 1.0


instance Monoid Str where
  mempty = ""
  source R from "R/core.R" ("paste" as mappend)

instance Monoid (List a) where
  mempty = []
  source R from "R/core.R" ("c" as mappend)


instance Foldable List where
  source R from "R/core.R" ("morloc_fold" as fold)

instance Functor List where
  source R from "R/core.R" ("morloc_map" as map)

instance Sequence List where
  empty = []
  source R from "R/core.R"
    ( "morloc_at" as at
    , "morloc_slice" as slice
    )


-------------------------------------------------------------------------------
--                 Pack and Unpack internal functions                          
-- These functions are used by morloc internally for (de)serialization of types
-- that cannot be automatically (de)serialized into/from JSON. They should not
-- be exported.
-------------------------------------------------------------------------------

-- WARNING: The Map implementation in R is total crap. R doesn't have a general
-- map type, at least not in base. The `list` type works properly only for
-- string keys. So the Map type here is mostly just for testing purposes.

instance Packable ([Str],[val]) (Map Str val) where
  source R from "R/data.R"
   ( "morloc_pack_map" as pack
   , "morloc_unpack_map" as unpack
   )

instance Packable (Int) Unit where
  source R from "R/data.R"
   ( "morloc_packUnit" as pack
   , "morloc_unpackUnit" as unpack
   )
